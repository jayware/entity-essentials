plugins {
    id 'me.champeau.gradle.jmh' version '0.3.1'
}

apply plugin: 'java'
apply plugin: 'osgi'

def versionPropsFile = file('build.properties')
def Properties versionProps = new Properties()
versionProps.load(new FileInputStream(versionPropsFile))
def buildNumber = versionProps['buildNumber'].toInteger() + 1
versionProps['buildNumber'] = buildNumber.toString()
versionProps.store(versionPropsFile.newWriter(), null)

version="${currentVersion}-${buildNumber}"

ext.projectObjectModel = pom {
    project {
        groupId project.group
        artifactId project.name
        version project.version
    }
    withXml {
        def root = asNode()
        root.appendNode('name', "${project.group}:${project.name}")
        root.appendNode('description', 'Default implementation of the Entity Essentials API')
        root.appendNode('url', 'http://jayware.github.io/entity-essentials/')
        root.appendNode('inceptionYear', '2015')
        def scm = root.appendNode('scm')
        scm.appendNode('url', 'https://github.com/jayware/entity-essentials')
        scm.appendNode('connection', 'git@github.com:jayware/entity-essentials.git')
        scm.appendNode('developerConnection', 'git@github.com:jayware/entity-essentials.git')
        def developers = root.appendNode('developers')
        def dev = developers.appendNode('developer')
        dev.appendNode('name', 'Elmar Schug')
        dev.appendNode('email', 'elmar.schug@jayware.org')
        dev.appendNode('organization', 'JayWare')
        dev.appendNode('organizationUrl', 'https://jayware.org')
        dev = developers.appendNode('developer')
        dev.appendNode('name', 'Markus Neubauer')
        dev.appendNode('email', 'markus.neubauer@jayware.org')
        dev.appendNode('organization', 'JayWare')
        dev.appendNode('organizationUrl', 'https://jayware.org')
        def license = root.appendNode('licenses').appendNode('license')
        license.appendNode('name', 'GNU Lesser General Public License 3.0')
        license.appendNode('url', 'http://www.gnu.org/licenses/')
        license.appendNode('distribution', 'repo')
    }
}

dependencies {

    compile project(':entity-essentials-api')

    compile 'org.ow2.asm:asm-all:5.1'
    compile "com.googlecode.concurrent-locks:concurrent-locks:1.0.0"

    testCompile 'org.testng:testng:6.9.13.6'
    testCompile 'org.assertj:assertj-core:3.5.2'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.jmockit:jmockit:1.28'
    testCompile 'ch.qos.logback:logback-classic:1.1.7'
}

test {
    useTestNG() {}
    testLogging {
        displayGranularity 4
        events "started", "passed", "skipped", "failed"
    }
}

jmh {
    jmhVersion = '1.12'
}

bintray {
    pkg {
        repo = 'maven'
        name = 'entity-essentials-impl'
        userOrg = 'jayware'
        licenses = ['LGPL-3.0']
        vcsUrl = 'https://github.com/jayware/entity-essentials'
        version {
            name = project.version
            desc = 'Default implementation of the Entity Essentials API'
            released  = new Date()
            vcsTag = project.version
        }
    }
    publications = ['e2Impl']
}

publishing {
    publications {
        e2Impl(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId project.name
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('name', "${project.group}:${project.name}")
                root.appendNode('description', 'Default implementation of the Entity Essentials API')
                root.appendNode('url', 'http://jayware.github.io/entity-essentials/')
                root.appendNode('inceptionYear', '2015')
                def scm = root.appendNode('scm')
                scm.appendNode('url', 'https://github.com/jayware/entity-essentials')
                scm.appendNode('connection', 'git@github.com:jayware/entity-essentials.git')
                scm.appendNode('developerConnection', 'git@github.com:jayware/entity-essentials.git')
                def developers = root.appendNode('developers')
                def dev = developers.appendNode('developer')
                dev.appendNode('name', 'Elmar Schug')
                dev.appendNode('email', 'elmar.schug@jayware.org')
                dev.appendNode('organization', 'JayWare')
                dev.appendNode('organizationUrl', 'https://jayware.org')
                dev = developers.appendNode('developer')
                dev.appendNode('name', 'Markus Neubauer')
                dev.appendNode('email', 'markus.neubauer@jayware.org')
                dev.appendNode('organization', 'JayWare')
                dev.appendNode('organizationUrl', 'https://jayware.org')
                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'GNU Lesser General Public License 3.0')
                license.appendNode('url', 'http://www.gnu.org/licenses/')
                license.appendNode('distribution', 'repo')
            }
        }
    }
}

artifactoryPublish {
    skip = false
    publications ('e2Impl')
}

task generatePom {
    def pomFile = "${buildDir}/tmp/jar/${project.name}-${version}.pom"
    group = 'Publishing'
    outputs.file pomFile
    doLast {
        projectObjectModel.writeTo(pomFile)
    }
}

jar {
    from (rootProject.projectDir) {
        include 'LICENSE.txt'
    }
    from (projectDir) {
        include 'README.md'
    }
    manifest {
        vendor 'jayware.org'
        name 'Entity Essentials IMPL'
        description 'Default implementation of the Entity Essentials API'
        symbolicName 'org.jayware.e2.impl'
        license 'http://www.gnu.org/licenses/lgpl-3.0.txt'
        instruction 'Bundle-Category', 'e2,entity-essentials,impl'
        instruction 'Bundle-RequiredExecutionEnvironment', 'JavaSE-1.6'
        instruction 'Service-Component', 'OSGI-INF/*.xml'
        instruction 'Export-Package', '!org.jayware.e2.*'
    }
    into("META-INF/maven/$project.group/$project.name") {
        from generatePom
        rename ".*", "pom.xml"
    }
}

task dist(type: Copy) {
    from jar
    from sourcesJar
    from javadocJar
    from generatePom
    into file("${rootProject.buildDir}/distribution")
}

clean {
    delete '.generated'
}