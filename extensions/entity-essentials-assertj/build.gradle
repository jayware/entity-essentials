apply plugin: 'java'
apply plugin: 'osgi'

def versionPropsFile = file('build.properties')
def Properties versionProps = new Properties()
versionProps.load(new FileInputStream(versionPropsFile))
def buildNumber = versionProps['buildNumber'].toInteger() + 1
versionProps['buildNumber'] = buildNumber.toString()
versionProps.store(versionPropsFile.newWriter(), null)

version="${currentVersion}"//-${buildNumber}"

ext.projectObjectModel = pom {
    project {
        groupId project.group
        artifactId project.name
        version project.version
    }
    withXml {
        def root = asNode()
        root.appendNode('name', "${project.group}:${project.name}")
        root.appendNode('description', 'Entity Essentials AssertJ is an extension which provides custom AssertJ asserations for testing code which uses the entity-essentials framework.')
        root.appendNode('url', 'http://jayware.github.io/entity-essentials/')
        root.appendNode('inceptionYear', '2016')
        def scm = root.appendNode('scm')
        scm.appendNode('url', 'https://github.com/jayware/entity-essentials')
        scm.appendNode('connection', 'git@github.com:jayware/entity-essentials.git')
        scm.appendNode('developerConnection', 'git@github.com:jayware/entity-essentials.git')
        def developers = root.appendNode('developers')
        def dev = developers.appendNode('developer')
        dev.appendNode('name', 'Elmar Schug')
        dev.appendNode('email', 'elmar.schug@jayware.org')
        dev.appendNode('organization', 'JayWare')
        dev.appendNode('organizationUrl', 'https://jayware.org')
        dev = developers.appendNode('developer')
        dev.appendNode('name', 'Markus Neubauer')
        dev.appendNode('email', 'markus.neubauer@jayware.org')
        dev.appendNode('organization', 'JayWare')
        dev.appendNode('organizationUrl', 'https://jayware.org')
        def license = root.appendNode('licenses').appendNode('license')
        license.appendNode('name', 'GNU Lesser General Public License 3.0')
        license.appendNode('url', 'http://www.gnu.org/licenses/')
        license.appendNode('distribution', 'repo')
    }
}

dependencies {

    compileOnly project(':entity-essentials-api')
    compileOnly 'org.assertj:assertj-core:3.6.1'

    testCompile project(':entity-essentials-api')
    testCompile 'org.assertj:assertj-core:3.6.1'

    testCompile 'org.testng:testng:6.10'
    testCompile 'org.jmockit:jmockit:1.29'

    testRuntime project(":entity-essentials-impl")
    testRuntime 'ch.qos.logback:logback-classic:1.1.8'
}

test {
    useTestNG() {}
    testLogging {
        displayGranularity 4
        events "started", "passed", "skipped", "failed"
    }
}

bintray {
    pkg {
        repo = 'maven'
        name = 'entity-essentials-assertj'
        userOrg = 'jayware'
        licenses = ['LGPL-3.0']
        vcsUrl = 'https://github.com/jayware/entity-essentials'
        version {
            name = project.version
            desc = 'Entity Essentials AssertJ is an extension which provides custom AssertJ asserations for testing code which uses the entity-essentials framework.'
            released  = new Date()
            vcsTag = project.version
        }
    }
    publications = ['e2AssertJ']
}

publishing {
    publications {
        e2AssertJ(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId project.name
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('name', "${project.group}:${project.name}")
                root.appendNode('description', 'Entity Essentials AssertJ is an extension which provides custom AssertJ asserations for testing code which uses the entity-essentials framework.')
                root.appendNode('url', 'http://jayware.github.io/entity-essentials/')
                root.appendNode('inceptionYear', '2016')
                def scm = root.appendNode('scm')
                scm.appendNode('url', 'https://github.com/jayware/entity-essentials')
                scm.appendNode('connection', 'git@github.com:jayware/entity-essentials.git')
                scm.appendNode('developerConnection', 'git@github.com:jayware/entity-essentials.git')
                def developers = root.appendNode('developers')
                def dev = developers.appendNode('developer')
                dev.appendNode('name', 'Elmar Schug')
                dev.appendNode('email', 'elmar.schug@jayware.org')
                dev.appendNode('organization', 'JayWare')
                dev.appendNode('organizationUrl', 'https://jayware.org')
                dev = developers.appendNode('developer')
                dev.appendNode('name', 'Markus Neubauer')
                dev.appendNode('email', 'markus.neubauer@jayware.org')
                dev.appendNode('organization', 'JayWare')
                dev.appendNode('organizationUrl', 'https://jayware.org')
                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'GNU Lesser General Public License 3.0')
                license.appendNode('url', 'http://www.gnu.org/licenses/')
                license.appendNode('distribution', 'repo')
            }
        }
    }
}

artifactoryPublish {
    skip = false
    publications ('e2AssertJ')
}

task generatePom {
    def pomFile = "${buildDir}/tmp/jar/${project.name}-${version}.pom"
    group = 'Publishing'
    outputs.file pomFile
    doLast {
        projectObjectModel.writeTo(pomFile)
    }
}

jar {
    from (rootProject.projectDir) {
        include 'LICENSE.txt'
    }
    from (projectDir) {
        include 'README.asciidoc'
    }
    manifest {
        vendor 'jayware.org'
        name 'Entity Essentials AssertJ'
        description 'Entity Essentials AssertJ is an extension which provides custom AssertJ asserations for testing code which uses the entity-essentials framework.'
        symbolicName 'org.jayware.e2.assertj'
        license 'http://www.gnu.org/licenses/lgpl-3.0.txt'
        instruction 'Bundle-Category', 'e2,entity-essentials,test'
        instruction 'Bundle-RequiredExecutionEnvironment', 'JavaSE-1.6'
        instruction 'Service-Component', 'OSGI-INF/*.xml'
        instruction 'Export-Package', '!org.jayware.e2.*'
    }
    into("META-INF/maven/$project.group/$project.name") {
        from generatePom
        rename ".*", "pom.xml"
    }
}

task dist(type: Copy) {
    from jar
    from sourcesJar
    from javadocJar
    from generatePom
    into file("${rootProject.buildDir}/distribution")
}

clean {
    delete '.generated'
}