apply plugin: "java"
apply plugin: "osgi"

dependencies {

    compile "com.google.guava:guava:19.0"
    compile "org.slf4j:slf4j-api:1.7.14"

    testCompile "org.testng:testng:6.9.10"
    testCompile "org.assertj:assertj-core:3.3.0"
    testCompile "org.mockito:mockito-core:1.10.19"
    testCompile "ch.qos.logback:logback-classic:1.1.3"
}

test {
    useTestNG() {}
}

jar {
    from (rootProject.projectDir) {
        include "LICENSE.txt"
    }
    from (projectDir) {
        include "README.md"
    }
    manifest {
        vendor "jayware.org"
        name "Entity Essentials API"
        description "Entity Essentials API"
        symbolicName "org.jayware.e2.api"
        license "http://www.gnu.org/licenses/lgpl-3.0.txt"
        instruction "Bundle-Category", "e2,entity-essentials,api"
        instruction "Bundle-RequiredExecutionEnvironment", "JavaSE-1.8"
    }
}

bintray {
    pkg {
        repo = 'maven'
        name = 'entity-essentials-api'
        userOrg = 'jayware'
        licenses = ['LGPL-3.0']
        vcsUrl = 'https://github.com/jayware/entity-essentials'
        version {
            name = project.version
            desc = 'Entity Essentials API'
            released  = new Date()
            vcsTag = project.version
        }
    }
    publications = ['e2Api']
}

publishing {
    publications {
        e2Api(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId project.name
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('name', "${project.group}:${project.name}")
                root.appendNode('description', 'Entity Essentials API')
                root.appendNode('url', 'http://jayware.github.io/entity-essentials/')
                root.appendNode('inceptionYear', '2015')
                def scm = root.appendNode('scm')
                scm.appendNode('url', 'https://github.com/jayware/entity-essentials')
                scm.appendNode('connection', 'git@github.com:jayware/entity-essentials.git')
                scm.appendNode('developerConnection', 'git@github.com:jayware/entity-essentials.git')
                def developers = root.appendNode('developers')
                def dev = developers.appendNode('developer')
                dev.appendNode('name', 'Elmar Schug')
                dev.appendNode('email', 'elmar.schug@jayware.org')
                dev.appendNode('organization', 'JayWare')
                dev.appendNode('organizationUrl', 'https://jayware.org')
                dev = developers.appendNode('developer')
                dev.appendNode('name', 'Markus Neubauer')
                dev.appendNode('email', 'markus.neubauer@jayware.org')
                dev.appendNode('organization', 'JayWare')
                dev.appendNode('organizationUrl', 'https://jayware.org')
                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'GNU Lesser General Public License 3.0')
                license.appendNode('url', 'http://www.gnu.org/licenses/')
                license.appendNode('distribution', 'repo')
            }
        }
    }
}